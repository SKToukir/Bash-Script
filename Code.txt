@echo off
setlocal EnableDelayedExpansion

set /p filepath="Enter File Path: "
set /p startPoint="Enter start point: "
set /p Dec="Enter Number of File: "
@REM set DecValue=4095

CD /d %filepath%

for /l %%a in (%startPoint%,1,%Dec%) do (
    
    @REM echo %filepath%
    call :ConvertDecToHex %%a HexValue
    @REM echo HexValue = %HexValue%
)

echo. >>info.txt File creation range (%startPoint% to %Dec%) 
echo. >>info.txt Total file: %Dec%
echo. >>info.txt %date% %time%

echo. File creation range (%startPoint% to %Dec%) 
echo. Total file: %Dec%
echo. %date% %time%

pause

:End
Exit
:: A function to convert Decimal to Hexadecimal
:: you need to pass the Decimal as first parameter
:: and return it in the second
:: This function needs setlocal EnableDelayedExpansion to be set at the start if this batch file
:ConvertDecToHex

set len=0
set LOOKUP=0123456789ABCDEF
set HEXSTR=
set PREFIX=
set FILE_NAME_PREFIX=70-B3-D5-E5-C
set NAME_DIVIDER=-

if "%1" EQU "" (
set "%2=0"
Goto:eof
)

set /a A=%1 || exit /b 1

if !A! LSS 0 set /a A=0xfffffff + !A! + 1 & set PREFIX=f

:loop

set /a B=!A! %% 16 & set /a A=!A! / 16
set HEXSTR=!LOOKUP:~%B%,1!%HEXSTR%
if %A% GTR 0 Goto :loop

set "%2=%PREFIX%%HEXSTR%"
set HOSTNAMETOUSE=%PREFIX%%HEXSTR%

CALL :strLen HOSTNAMETOUSE len

@REM echo %len%

if !len! EQU 1 (
    set HOSTNAMETOUSE=!FILE_NAME_PREFIX!0!NAME_DIVIDER!0!HOSTNAMETOUSE!
) else if !len! EQU 2 (
    set HOSTNAMETOUSE=!FILE_NAME_PREFIX!0!NAME_DIVIDER!!HOSTNAMETOUSE!
) else if !len! EQU 3 (
    set strPre=%HOSTNAMETOUSE:~0,1%
    set strPost=%HOSTNAMETOUSE:~-2%
    set HOSTNAMETOUSE=!FILE_NAME_PREFIX!!strPre!!NAME_DIVIDER!!strPost!
) else (
    set HOSTNAMETOUSE=%HOSTNAMETOUSE%
)

echo %Dec% of %1 :Successfully generate %HOSTNAMETOUSE%
@REM echo %filepath%
CD /d %filepath%

::Create file
echo. 2>%HOSTNAMETOUSE%

Goto:eof
:: End of :ConvertDecToHex function

:: Below function calculate the length of string
:strLen  strVar  [rtnVar]
setlocal disableDelayedExpansion
set len=0
if defined %~1 for /f "delims=:" %%N in (
  '"(cmd /v:on /c echo(!%~1!&echo()|findstr /o ^^"'
) do set /a "len=%%N-3"
endlocal & if "%~2" neq "" (set %~2=%len%) else echo %len%
:: End of calculate length method

Goto:eof


**Note: Save this file as .bat extension